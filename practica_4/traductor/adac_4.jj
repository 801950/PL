//*****************************************************************
// File:   adac.jj
// Author: Nerea Gallego, César Borja
// Date:   ...........
// Coms:   Un tutorial sobre el análisis léxico con javacc
//         https://javacc.github.io/javacc/tutorials/token-manager.html
//*****************************************************************

options {
   DEBUG_PARSER = false;
}

PARSER_BEGIN(adac_4)
package traductor;

import java.util.*;
import java.lang.Integer.*;
import java.lang.Exception.*;
import java.io.IOException;
import java.lang.String;
import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;
import lib.errores.*;
import lib.attributes.*;
import lib.tools.codeGeneration.CodeBlock;
import lib.tools.codeGeneration.PCodeInstruction.OpCode;
import lib.tools.*;
import java.util.Stack;
import lib.tools.codeGeneration.PCodeInstruction;
import java.io.FileWriter;


public class adac_4 {

	static SymbolTable ts = new SymbolTable();
//	static ErrorSemantico err =  new ErrorSemantico();
	static SemanticFunctions semFuncs = new SemanticFunctions();

	static Stack<Symbol> stack = new Stack<Symbol>();
	
	static SymbolProcedure principal;

	static FileWriter f = null;

    public static void main(String[] args) {
    	adac_4 parser;
		
		try{
			f = new FileWriter(args[0]+".pcode");
		} catch(IOException e) {
			System.out.println(e);
		}

    	try {
			
	    	if(args.length == 0) { //entrada desde stdin
				parser = new adac_4(System.in);
			}
			else { //entrada desde fichero en args[0]
	            parser = new adac_4(new java.io.FileInputStream(args[0])); 
			}
			parser.S(); //invoca símbolo inicial de la gramática
		}
		catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + args[0] + " no encontrado.");
		}
		catch (TokenMgrError e) {
        	System.err.println("LEX_ERROR: " + e.getMessage());
        }
        catch (ParseException e) {
        	System.err.println("SINT_ERROR: " + e.getMessage());
        }
		try{
			f.close();
		} catch(IOException e){
			System.err.println("ERROR CON EL FICHERO: " + e.getMessage());
		}
    }
}

PARSER_END(adac_4)
//------------ descripción del AL
SKIP:
{
	" "
|	"\t"
|	"\n"
	
}
//---------------------------------------------------------
TOKEN_MGR_DECLS : {
	//lo declarado aquí estará en "ejemploTokenManager"
	//también estará, por ejemplo, "lengthOfMatch", "image", ...
	static int nPalInt = 0;
	static int valNums = 0;
	static void infoToken(String mensaje, Token token){
		System.out.println("(" + token.beginLine + "," + token.beginColumn + "): " + mensaje + " " + token.image);
	}
}

SPECIAL_TOKEN : {
	< tCOM: "--"(~["\n"])*"\n" >
}

TOKEN:
{
 	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
|	< #ALNUM: (< LETTER > | < DIGIT >) >
}

TOKEN:
{
	< tPOPEN: "(" >
|	< tPCLOSE: ")" >	
|	< tC2 : "\"" >
|	< tC1 : "\'" >
|	< tCOMA : "," >
|	< tPC: ";" >
|	< tCORCHETEOPEN : "[">
|	< tCORCHETECLOSE : "]" >
|	< tNUM : (< DIGIT >)+ >

}

TOKEN:
{
 	< tBEGIN: "begin" >
|	< tBOOL: "boolean" >
|	< tCHAR: "character" >
| 	< tINT: "integer" >
|	< tFUNC: "function" >
|	< tPROC: "procedure" >
|	< tIS : "is" >
|	< tVAL :  "val" >
|	< tREF : "ref" >
|	< tRET : "return" >
|	< tINT2CHAR : "int2char" >
|	< tCHAR2INT : "char2int" >
| 	< tSkL : "skip_line" >
}

TOKEN:
{
	< tIF: "if" >
|	< tTHEN: "then" >
|	< tELSE: "else" >
|	< tEND: "end" >
|	< tWHILE: "while" >
|	< tDO: "do" >
}

TOKEN:
{
	< tASIG: ":=" >
|	< tPUTLINE: "put_line" >
|	< tPUT: "put" >
|	< tGET: "get" >
}

TOKEN: 
{
	< tAND: "and">
|	< tOR: "or">
|	< tNOT: "not" >
|	< tMAYEQ: ">=" >
|	< tMENEQ: "<=" >
|	< tEQ: "=" >
|	< tMAY: ">" >
|	< tMEN : "<" >
|	< tDIST : "<>" >
|	< tTRUE : "true" >
|	< tFALSE : "false" >
}

TOKEN: 
{
	< tSUM: "+">
|	< tRES: "-">
|	< tMUL: "*">
|	< tDIV: "div">	
|	< tMOD: "mod">
|	< tSTRING : "\"" ("\"\"")* (("\"\"")* (~["\""])*)* "\"" >
|	< tCARACTER : "\'" (~[])? "\'" >
}


TOKEN:
{
	< tID:  (< LETTER > | ("_"(< ALNUM >)+)+ ) ( < ALNUM > | "_"(< ALNUM >)+ )* >
}

SPECIAL_TOKEN : {
	< tERROR: ~[] >
		{
			System.out.println("ERROR LÉXICO: (" + matchedToken.beginLine + "," + matchedToken.beginColumn + "): símbolo no reconocido " + matchedToken.image);
		}
}

void S () : 
{

}
{
	prog()
	< EOF >
}

//------------ Símbolo inicial de la gramática. Para análisis léxico no hace falta más
void prog () : 
{
	Token t;
	CodeBlock codeOut = new CodeBlock();
	String s;
	Attributes at = new Attributes();
}
{
  	<tPROC>
		{
			codeOut.addInst(PCodeInstruction.OpCode.ENP,"L0");
			s = codeOut.toString();
			escribirCodigo(s);
			codeOut.clearBlock();
			
		}
  	t = <tID>
		{
			
			principal = semFuncs.insertProcedureSymbolTabFirst(ts, t);
			at.lastLabel = 0;
			at.dir = 3;
		}
  	<tIS>
 	declaracion_variables(at)
  	declaracion_procs_funcs(at)
		{
		//	codeOut.addBlock(at.code);
			at.code.addLabel("L0");
			//codeOut.clearBlock();
		}
	bloque_sentencias(at)
		{
			codeOut.addBlock(at.code);
			codeOut.addInst(PCodeInstruction.OpCode.LVP);
			s = codeOut.toString();
			escribirCodigo(s);
			codeOut.clearBlock();
			System.err.println(ts.toString());
		}
}

void declaracion_variables(Attributes at):
{

	Attributes at1 = new Attributes();
	at1.dir = at.dir;
}
{
	( declaracion(at1) <tPC> )*
	{
		at1.code.encloseXMLTags("variable_declaration");
		at.code.addBlock(at1.code);
		at.dir = at1.dir;
	}
}

void declaracion(Attributes at):
{
	Symbol s = null;
	Token t1;

}
{
	tipo_variable(at) 
	lista_vars(at)
	{
		//at.code.addBlock(at1.code);
		//escribirCodigo(at.code.toString());
	}
}
void tipo_variable(Attributes at):
{

}
{
	<tINT> 
		{
			at.type = Symbol.Types.INT;
		}
| 	<tCHAR> 
		{
			at.type = Symbol.Types.CHAR;
		}
| 	<tBOOL>
		{
			at.type = Symbol.Types.BOOL;
		}
}

void lista_vars(Attributes at):
{

	
}
{
	variable(at) ( <tCOMA> variable(at) )*

}

void variable(Attributes at):
{
	Token t1, t2;	
}
{
	LOOKAHEAD(2)
	t1 = <tID> <tCORCHETEOPEN> t2 = <tNUM> <tCORCHETECLOSE>
		{
			semFuncs.insertArraySymbolTab(ts,t1,at,t2);
			at.dir = at.dir + Integer.parseInt(t2.image);
		}
| 	t1 = <tID>
		{
			semFuncs.insertVariableSymbolTab(ts,t1,at);
			at.dir ++;
		}
}

void declaracion_procs_funcs(Attributes at):
{

}
{
	( declaracion_proc_func(at) )*
}



void declaracion_proc_func(Attributes at):
{

}
{

	declaracion_procedimientos(at)
|	declaracion_funciones(at)
	
}

void declaracion_procedimientos(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	at3.dir = 3;
	//at1.lastLabel = at.lastLabel;
	int n = at.lastLabel + 1, n2;
	CodeBlock code1 = new CodeBlock();
	CodeBlock code2 = new CodeBlock();
	at.funcDirection = n;
}
{
	cabecera_procedimiento(at)
		{
			code1.addLabel("L"+n);
			at.lastLabel ++;
			
			n2 = at.lastLabel + 1;
			at.lastLabel++;
			at1.lastLabel = at.lastLabel;
			code1.addInst(PCodeInstruction.OpCode.JMP, "L"+n2);
		} 
	declaracion_variables(at3)
		{
			code2.addBlock(at3.code);
			code1.addBlock(code2);
			code2.clearBlock();
			at1.dir = at3.dir;
		}
	(declaracion_proc_func(at1))*
		{
			code1.addBlock(at1.code);
			at.lastLabel = at1.lastLabel;
			at2.lastLabel = at.lastLabel;
			at2.dir = at3.dir;
		}
	bloque_sentencias(at2)
		{
			code1.addLabel("L"+n2);
			code2.addBlock(at2.code);
			code2.addInst(PCodeInstruction.OpCode.CSF);
			code2.encloseXMLTags("instruction_block");
			code1.addBlock(code2);
			code1.encloseXMLTags("procedure");
			at.code.addBlock(code1);
			ts.removeBlock();
			stack.pop();
			at.lastLabel = at2.lastLabel;
		}
}

void declaracion_funciones(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	at3.dir = 3;
	//at1.lastLabel = at.lastLabel;
	int n = at.lastLabel + 1, n2;
	CodeBlock code1 = new CodeBlock();
	CodeBlock code2 = new CodeBlock();
	at.funcDirection = n;
}
{
	cabecera_funcion(at) 
		{
			code1.addLabel("L"+n);
			at.lastLabel ++;
			
			n2 = at.lastLabel + 1;
			at.lastLabel++;
			at1.lastLabel = at.lastLabel;
			code1.addInst(PCodeInstruction.OpCode.JMP, "L"+n2);
		}
	declaracion_variables(at3)
		{
			code2.addBlock(at3.code);
			code1.addBlock(code2);
			code2.clearBlock();
		//	at1.dir = at3.dir;
		}
	(declaracion_proc_func(at1))* 
		{
			code1.addBlock(at1.code);
			at.lastLabel = at1.lastLabel;
			at2.lastLabel = at.lastLabel;
			at2.dir = at3.dir;
		}
	bloque_sentencias(at2)
		{
			
			code1.addLabel("L"+n2);
			code2.addBlock(at2.code);
			code2.addInst(PCodeInstruction.OpCode.CSF);
			code2.encloseXMLTags("instruction_block");
			code1.addBlock(code2);
			code1.encloseXMLTags("procedure");
			at.code.addBlock(code1);
			ts.removeBlock();
			stack.pop();
			at.lastLabel = at2.lastLabel;			
		}
}

void cabecera_procedimiento(Attributes at):
{
	Token t;
}
{
	<tPROC> t = <tID> 
		{
			semFuncs.insertProcedureSymbolTab(ts,t, at.funcDirection);
			stack.push(ts.getSymbol(t.image));
			at.token = t;
			at.type = Symbol.Types.PROCEDURE;
		}
	<tPOPEN> lista_parametros(at) <tPCLOSE> <tIS>
		
}

void cabecera_funcion(Attributes at):
{
	Token t;
	Symbol.Types tipo;
}
{
	<tFUNC> tipo_variable(at) t = <tID>
		{
			semFuncs.insertFunctionSymbolTab(ts,t,at,at.funcDirection);
			stack.push(ts.getSymbol(t.image));
			at.token = t;
			at.type = Symbol.Types.FUNCTION;
		}
	<tPOPEN> lista_parametros(at) <tPCLOSE> <tIS>
}

void lista_parametros(Attributes at):
{

}
{
	(parametro(at) (<tPC> parametro(at))*)?
}

void parametro(Attributes at):
{
	Symbol.Types tipo;
	Attributes at1 = new Attributes();
	Token t;
}
{
	( <tVAL>
		{
			semFuncs.insertParameterType(at1,Symbol.ParameterClass.VAL);
		} 
	| <tREF>
		{
			semFuncs.insertParameterType(at1,Symbol.ParameterClass.REF);
		})
	
 
	tipo_variable(at1) lista_parametros_formales(at,at1)
}

void lista_parametros_formales(Attributes at,Attributes at1):
{

}
{
	parametro_formal(at,at1) ( <tCOMA> parametro_formal(at,at1) )*
}

void parametro_formal(Attributes at, Attributes at1):
{
	Token t1, t2;
}
{
	LOOKAHEAD(2)
	t1 = <tID> <tCORCHETEOPEN> t2 = <tNUM> <tCORCHETECLOSE>
		{
			semFuncs.insertVector(ts,t1,t2,at,at1);
		//	semFuncs.insertArraySymbolTab(ts,t1,at,t2);
		}
| 	t1 = <tID>
		{
			semFuncs.insertParametro(ts,at,at1,t1);
		//	semFuncs.insertVariableSymbolTab(ts,t1,at);
		}
}

void bloque_sentencias(Attributes at):
{
	//Attributes at1 = new Attributes();
}
{
	try {
		<tBEGIN>
		(inst(at))*
		<tEND>
		{
		}
	} catch (ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tEND);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void inst(Attributes at):
{

}
{
	inst_acaban_pc(at) 
|	inst_acaban_end(at)
}

void inst_acaban_pc(Attributes at):
{
	//Attributes at1 = new Attributes();
}
{
	try {
		(inst_leer(at)
		|	inst_saltar_linea(at)
		|	inst_escribir(at)
		|	inst_escribir_linea(at)
		|	LOOKAHEAD(2) inst_invoc_proc(at)
		|	inst_asignacion(at)
		|	inst_return(at)
		)
			<tPC>
			{
				//at.code.addBlock(at1.code);
			}
	} catch(ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPC);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void inst_acaban_end(Attributes at):
{
	Attributes at1 = new Attributes();
}
{
	try{
		(inst_iteracion(at)
	|	inst_seleccion(at) ) <tEND>
			{
				//at.code.addBlock(at1.code);
			}
	} catch(ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tEND);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}



void inst_asignacion(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes();
	CodeBlock codeOut = new CodeBlock();
	Symbol s;
	at2.dir = at.dir;
	at1.dir = at.dir;
}
{
	asignable(at1)
	<tASIG>
	expresion(at2)
		{
			semFuncs.checkAsignacion(at1,at2);
			codeOut.addBlock(at1.code);
			codeOut.addBlock(at2.code);
			codeOut.addInst(PCodeInstruction.OpCode.ASG);
			codeOut.encloseXMLTags("Asignacion_a_variable_simple_"+at1.token.image);
			at.code.addBlock(codeOut);
			//escribirCodigo(codeOut.toString());
		}
}

void asignable(Attributes at):
{
	Attributes at1 = new Attributes();
	Token t;
	Symbol s;
}
{
	LOOKAHEAD(2)
	t = <tID> <tCORCHETEOPEN> expresion(at1) <tCORCHETECLOSE>
		{
			semFuncs.checkAsignable(ts,t,at1,at);
			s = ts.getSymbol(t.image);
			at.code.addBlock(at1.code);
			at.code.addComment(" Initial address of array \"" + t.image +"\"");
			at.code.addInst(PCodeInstruction.OpCode.SRF,stack.size()-s.nivel,(int)s.dir);
			at.code.addInst(PCodeInstruction.OpCode.PLUS);		
		}
| 	t = <tID>
		{
			semFuncs.checkAsignable(ts,t,at);
			s = ts.getSymbol(t.image);
			at.code.addComment(" Address of variable / parameter \"" + t.image +"\"");
			at.code.addInst(PCodeInstruction.OpCode.SRF,stack.size()-s.nivel,(int)s.dir);
		}
		
}

void inst_iteracion(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes();
	CodeBlock codeOut = new CodeBlock();
	int n = at.lastLabel + 1,n2;
	CodeBlock code2 = new CodeBlock();
}
{
	<tWHILE>
	expresion(at1)
		{
			semFuncs.checkBool(at1);
			codeOut.addLabel("L"+n);
			at.lastLabel++;
			code2.addBlock(at1.code);
			code2.encloseXMLTags("condition");
			codeOut.addBlock(code2);
			code2.clearBlock();
			n2 = at.lastLabel+1;
			at.lastLabel++;
			codeOut.addInst(PCodeInstruction.OpCode.JMF, "L"+n2);
			at2.lastLabel = at.lastLabel;
			at2.dir = at.dir;
		}
	<tDO>
	(inst(at2))*
		{
			code2.addBlock(at2.code);
			at.lastLabel = at2.lastLabel;
			code2.encloseXMLTags("while_instruction_block");
			code2.addInst(PCodeInstruction.OpCode.JMP,"L"+n);
			code2.addLabel("L"+n2);
			codeOut.addBlock(code2);
			codeOut.encloseXMLTags("while");
			at.code.addBlock(codeOut);
		}
}

void inst_return(Attributes at):
{
	Attributes at1 = new Attributes();
	at1.dir = at.dir;
	Symbol s;
	SymbolFunction sf = new SymbolFunction("",null,null);
	Token t;
	CodeBlock code1 = new CodeBlock();
}
{
	t = <tRET>	expresion(at1)
		{
			semFuncs.checkFunction(stack, at1);
			code1.addBlock(at1.code);
			code1.addInst(PCodeInstruction.OpCode.CSF);
			code1.encloseXMLTags("return");
			at.code.addBlock(code1);
		}
}

void inst_leer(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes();
	CodeBlock codeOut = new CodeBlock();
}
{
	try {
		<tGET> <tPOPEN> lista_asignables(at1) <tPCLOSE>
			{
				for(Attributes a : at1.par){
					codeOut.addBlock(a.code);
					codeOut.addInst(PCodeInstruction.OpCode.RD,(a.type==Symbol.Types.CHAR)?0:1);
				}
				at.code.addBlock(codeOut);
			//	escribirCodigo(codeOut.toString());
			}
	} catch(ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void lista_asignables(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes();
}
{
	asignable(at1) (<tCOMA> lista_asignables(at2))?
		{
			semFuncs.checkAsignableGet(at1);
			at.par.add(at1);
			at.par.addAll(at2.par);
		}
}

void inst_escribir(Attributes at):
{
	Attributes at1 = new Attributes();
	at1.dir = at.dir;
	CodeBlock codeOut = new CodeBlock();
}
{
    try {
		<tPUT> <tPOPEN> lista_una_o_mas_exps(at1)
    	<tPCLOSE>
		{
			try{
				codeOut.clearBlock();
				for(Attributes a : at1.par){
					
					if(a.type == Symbol.Types.STRING){
						a.code.encloseXMLTags("put");
						codeOut.addComment(" Put STRING");
						codeOut.addBlock(a.code);
					} else if(a.type == Symbol.Types.INT){
						a.code.addComment(" Put INTEGER");
						a.code.addInst(PCodeInstruction.OpCode.WRT,1);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);
					} else if(a.type == Symbol.Types.CHAR){
						a.code.addComment(" Put CHAR");
						a.code.addInst(PCodeInstruction.OpCode.WRT,0);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);
					} else if(a.type == Symbol.Types.BOOL){
						a.code.addComment(" Put BOOL");
						a.code.addInst(PCodeInstruction.OpCode.WRT,1);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);
					}
				}
				codeOut.encloseXMLTags("put");
				at.code.addBlock(codeOut);
			} catch(Exception e){
				System.err.println("Excepción: " + e.getMessage());
			}
		}
	} catch(ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void inst_escribir_linea(Attributes at):
{
	Attributes at1 = new Attributes();
	CodeBlock codeOut = new CodeBlock();
	at1.dir = at.dir;
}
{
    try {
		<tPUTLINE> <tPOPEN> (lista_una_o_mas_exps(at1))?
    	<tPCLOSE>
		{
			try{
				for(Attributes a : at1.par){
					
					if(a.type == Symbol.Types.STRING){
						a.code.encloseXMLTags("put");
						codeOut.addComment(" Put STRING");
						codeOut.addBlock(a.code);
					} else if(a.type == Symbol.Types.INT){
						a.code.addComment(" Put INTEGER");
						a.code.addInst(PCodeInstruction.OpCode.WRT,1);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);
					} else if(a.type == Symbol.Types.CHAR){
						a.code.addComment(" Put CHAR");
						a.code.addInst(PCodeInstruction.OpCode.WRT,0);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);

					} else if(a.type == Symbol.Types.BOOL){			
						a.code.addComment(" Put BOOL");
						a.code.addInst(PCodeInstruction.OpCode.WRT,1);
						a.code.encloseXMLTags("put");
						codeOut.addBlock(a.code);
					}
				}
				codeOut.addComment(" CR/LF");
				codeOut.addInst(PCodeInstruction.OpCode.STC,13);
				codeOut.addInst(PCodeInstruction.OpCode.WRT,0);
				codeOut.addInst(PCodeInstruction.OpCode.STC,10);
				codeOut.addInst(PCodeInstruction.OpCode.WRT,0);
				codeOut.encloseXMLTags("put_line");
				at.code.addBlock(codeOut);
			} catch(Exception e){
				System.err.println("Excepción: " + e.getMessage());
			}
		}
	} catch(ParseException e) {
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void inst_invoc_proc(Attributes at):
{
	Attributes at1 = new Attributes();
	Token t;
	Symbol s;
	CodeBlock code1 = new CodeBlock();
}
{
    try {
		t = <tID> <tPOPEN> (lista_una_o_mas_exps(at1))? <tPCLOSE>
			{
				semFuncs.comprobarNoPrincipal(principal,t);
				semFuncs.comprobacionParametros(at1,t,ts);
				s = ts.getSymbol(t.image);
				code1.addOSFInst((int)at.dir,stack.size()-s.nivel,"L"+s.dir);
				code1.encloseXMLTags("procedure_call_"+t.image);
				at.code.addBlock(code1);
			}
	} catch (ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void inst_seleccion(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	int n = at.lastLabel +1,n2;
	CodeBlock codeOut = new CodeBlock();
	CodeBlock code2 = new CodeBlock();
}
{
	/**
	 * FALTARIA DE PASAR INFO DE LAST LABEL Y ULTIMA DIRECCIÓN A at1, at2 y at3 */
	<tIF> 
	expresion(at1) 
		{
			semFuncs.checkBool(at1);
			at1.lastLabel = at.lastLabel;
			at1.dir = at.dir;
			codeOut.addBlock(at1.code);
			codeOut.encloseXMLTags("condition");
			codeOut.addInst(PCodeInstruction.OpCode.JMF, "L"+n);
			code2.addBlock(codeOut);
			at.lastLabel++;
			at2.lastLabel = at.lastLabel;
			at2.dir = at.dir;
		}
	<tTHEN> (inst(at2))*
	{
		codeOut.clearBlock();
		codeOut.addBlock(at2.code);
		n2 = at2.lastLabel + 1;
		at.lastLabel = at2.lastLabel+1;
		
		codeOut.addInst(PCodeInstruction.OpCode.JMP, "L"+n2);
		codeOut.encloseXMLTags("then");
		code2.addBlock(codeOut);
		at3.lastLabel = at.lastLabel;
		at3.dir = at.dir;
	}
	(<tELSE> (inst(at3))* )? 
	{
		codeOut.clearBlock();
		codeOut.addLabel("L"+n);
		codeOut.addBlock(at3.code);
		codeOut.encloseXMLTags("else");
		codeOut.addLabel("L"+n2);
		code2.addBlock(codeOut);
		code2.encloseXMLTags("if-then");
		at.code.addBlock(code2);
	}
}

void inst_saltar_linea(Attributes at):
{
	CodeBlock code1 = new CodeBlock();
	int n = at.lastLabel + 1;
	at.lastLabel++;
}
{
	try {
		<tSkL> <tPOPEN> <tPCLOSE>
			{
				code1.addLabel("L"+n);
				code1.addComment(" Temporary hidden compiler variable used for reading until LF");
				code1.addInst(PCodeInstruction.OpCode.SRF,stack.size(),(int)at.dir);
				code1.addInst(PCodeInstruction.OpCode.RD,0);
				code1.addInst(PCodeInstruction.OpCode.SRF,stack.size(),(int)at.dir);
				code1.addInst(PCodeInstruction.OpCode.DRF);
				code1.addInst(PCodeInstruction.OpCode.STC,10);
				code1.addInst(PCodeInstruction.OpCode.EQ);
				code1.addInst(PCodeInstruction.OpCode.JMF,"L"+n);
				code1.encloseXMLTags("skip_line");
				at.code.addBlock(code1);
			}
	} catch (ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

void lista_cero_o_mas_exps():
{
	Attributes at = new Attributes();
}
{
	( lista_una_o_mas_exps(at) )?
	
}

void lista_una_o_mas_exps(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	at1.dir = at.dir;
	at2.dir = at.dir;
}
{
	expresion(at1) (at3.token = <tCOMA> lista_una_o_mas_exps(at2))?
		{
			semFuncs.saveInfoParameter(at,at1,at2);
		//	at.code.addBlock(at1.code);
		//	System.out.println("Añado el boque " + at.code.toString());
		}
}

void expresion(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	at1.dir = at.dir;
	at2.dir = at.dir;
}
{
	expresion_simple(at1) ( operador_relacional(at3) expresion_simple(at2) )?
		{
			semFuncs.check2typesWithRelationalOperator(at1, at2, at3, at);
		}
}

void operador_relacional(Attributes at):
{
	Token t;
}
{
	t = <tEQ>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.EQ);
		}
|	t = <tMAYEQ>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.GTE);
		}
|	t = <tMENEQ>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.LTE);
		}
|	t = <tMAY>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.GT);
		}
|	t = <tMEN>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.LT);
		}
|	t = <tDIST>
		{
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.NEQ);
		}
}

void expresion_simple(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	at1.dir = at.dir;
	at2.dir = at.dir;
	Token t = new Token();
}
{
	(t = <tRES> | <tSUM>)?
	termino(at1) (operador_aditivo(at3) termino(at2))*
		{
			if(t.image != null){
				at1.code.addInst(PCodeInstruction.OpCode.NGI);
			}
			semFuncs.check2typesWithOperator(at1,at2,at3,at);
		}
}

void operador_aditivo(Attributes at):
{
	Token t;
}
{
	t = <tSUM>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.PLUS);
		}
|	t = <tRES>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.SBT);
		}
| 	t = <tOR>
		{
			at.type = Symbol.Types.BOOL;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.OR);
		}
}

void termino(Attributes at):
{
	Attributes at1 = new Attributes(), at2 = new Attributes(), at3 = new Attributes();
	Token t;
	at1.dir = at.dir;
	at2.dir = at.dir;
	Symbol.Types expected;
	System.out.println("direccion: " + at1.dir);
}
{
	factor(at1) (operador_multiplicativo(at3) factor(at2))*
		{
			semFuncs.check2typesWithOperator(at1,at2,at3,at);
		}
}


void operador_multiplicativo(Attributes at):
{
	Token t;
}
{
	t = <tMUL>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.TMS);
		}
|	t = <tMOD>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.MOD);
		}
|	t = <tDIV>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.DIV);
		}
|	t = <tAND>
		{
			at.type = Symbol.Types.BOOL;
			at.token = t;
			at.code.addInst(PCodeInstruction.OpCode.AND);
		}
}

void factor(Attributes at):
{
	Token t;
	Symbol s;
	Symbol.Types tipo;
}
{
	 
	<tNOT> factor(at)
		{
			semFuncs.checkBool(at);
			at.constante = true;
			at.code.addInst(PCodeInstruction.OpCode.NGB);
		}
|	<tPOPEN> expresion(at) <tPCLOSE>
| 	LOOKAHEAD(2) factor_con_par(at)
|	LOOKAHEAD(2) t = <tID> <tCORCHETEOPEN> expresion(at) <tCORCHETECLOSE>
		{
			semFuncs.checkArray(ts,t,at);
			at.constante = false;
		//	at.code.addBlock(at.code);
			s = ts.getSymbol(t.image);
			at.code.addComment(" Initial address of array \"" + t.image + "\"");
			at.code.addInst(PCodeInstruction.OpCode.SRF,stack.size()-s.nivel,(int)s.dir);
			at.code.addInst(PCodeInstruction.OpCode.PLUS);
			at.code.addInst(PCodeInstruction.OpCode.DRF);
		}
|	t = <tID>
		{
			at.token = t;
			semFuncs.checkVariable(ts,t,at);
			at.constante = false;
			s = ts.getSymbol(t.image);
			at.code.addComment("Variable / parameter \"" + t.image + "\"");
			at.code.addInst(PCodeInstruction.OpCode.SRF,stack.size()-s.nivel,(int)s.dir);
			at.code.addInst(PCodeInstruction.OpCode.DRF);
		}
| 	t = <tNUM>
		{
			at.type = Symbol.Types.INT;
			at.token = t;
			at.constante = true;
			at.code.addInst(PCodeInstruction.OpCode.STC,Integer.valueOf(token.image));
		}
| 	t = <tCARACTER>
		{
			at.type = Symbol.Types.CHAR;
			at.token = t;
			at.constante = true;
			String str = at.token.image;
			//str = str.substring(1,str.length()-1);
		//	for(int i = 1; i < str.length()-1; i++){
			at.code.addInst(PCodeInstruction.OpCode.STC,(int)str.charAt(1));
		//	}
		}
| 	t = <tSTRING>
		{
			at.type = Symbol.Types.STRING;
			at.token = t;
			at.constante = true;
			String str = at.token.image;
			//str = str.substring(1,str.length()-1);
			for(int i = 1; i < str.length()-1; i++){
				at.code.addInst(PCodeInstruction.OpCode.STC,(int)str.charAt(i));
				at.code.addInst(PCodeInstruction.OpCode.WRT,0);
			}
		}
| 	t = <tTRUE>
		{
			at.type = Symbol.Types.BOOL;
			at.token = t;
			at.constante = true;
			at.code.addInst(PCodeInstruction.OpCode.STC,1);
		}
| 	t = <tFALSE>
		{
			at.type = Symbol.Types.BOOL;
			at.token = t;
			at.constante = true;
			at.code.addInst(PCodeInstruction.OpCode.STC,0);
		}
}

void factor_con_par(Attributes at):
{
	Token t;
	Attributes at1 = new Attributes();
	CodeBlock code1 = new CodeBlock();
	Symbol s;
}
{
	try {
		<tINT2CHAR> <tPOPEN> expresion(at) <tPCLOSE> 
			{
				semFuncs.checkInt(at);
				at.type = Symbol.Types.CHAR;
				at.constante = true;
			}
		|	<tCHAR2INT> <tPOPEN> expresion(at) <tPCLOSE>
			{
				semFuncs.checkChar(at);
				at.type = Symbol.Types.INT;
				at.constante = true;
			}
		|	LOOKAHEAD(2) t = <tID> <tPOPEN> (lista_una_o_mas_exps(at1))? <tPCLOSE>
			{
				at.token = t;
				at.constante = true;
				semFuncs.checkFunction(ts,t,at);
				semFuncs.comprobacionParametros(at1,t,ts);
				s = ts.getSymbol(t.image);
				code1.addOSFInst((int)at.dir,stack.size()-s.nivel,"L"+s.dir);
				code1.encloseXMLTags("function_call_"+t.image);
				at.code.addBlock(code1);

			}
	} catch(ParseException e){
		Set<Integer> conjSinc = infoParseException(e);
		conjSinc.add(tPCLOSE);
		recuperacionPanico(e.currentToken.next,conjSinc);
	}
}

JAVACODE
Set<Integer> infoParseException(ParseException e){
	Set<Integer> esperados = new HashSet<Integer>();
	System.err.println("ERROR SINT: ('" + e.currentToken.next.image + "'," + e.currentToken.next.beginLine + "," + e.currentToken.next.beginColumn + ")");
	System.err.println("Se esperaba uno de los siguientes tokens:");
	for (int i = 0; i < e.expectedTokenSequences.length ; i++){
		esperados.add(e.expectedTokenSequences[i][0]);
		System.err.println("\t" + adac_4Constants.tokenImage[e.expectedTokenSequences[i][0]]);
	}
	return esperados;
}

JAVACODE
void recuperacionPanico(Token tInesperado, Set<Integer> conjSinc){
	System.err.println("----> Recupero modo pánico: " + "\n----> Saltando todo hasta token de conjunto de sincronización");
	Token t = getNextToken();
	while(!conjSinc.contains(t.kind) && t.kind != EOF){
		System.err.println("Descartando token (" + adac_4Constants.tokenImage[t.kind] + "," + t.image + ")");
		t = getNextToken();
	}
}

JAVACODE
void escribirCodigo(String s){
	try{
		for (int i = 0; i < s.length(); i++)
            f.write(s.charAt(i));
	} catch(IOException e){
		System.err.println("Exception: " + e.getMessage());
	}
}