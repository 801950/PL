//*****************************************************************
// File:   adac.jj
// Author: Nerea Gallego, César Borja
// Date:   ...........
// Coms:   Un tutorial sobre el análisis léxico con javacc
//         https://javacc.github.io/javacc/tutorials/token-manager.html
//*****************************************************************

options {
   DEBUG_PARSER = true;
}

PARSER_BEGIN(adac)
package traductor;

public class adac {
    public static void main(String[] args) {
    	adac parser;

    	try {
	    	if(args.length == 0) { //entrada desde stdin
				parser = new adac(System.in);
			}
			else { //entrada desde fichero en args[0]
	            parser = new adac(new java.io.FileInputStream(args[0])); 
			}
			parser.S(); //invoca símbolo inicial de la gramática
		}
		catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + args[0] + " no encontrado.");
		}
		catch (TokenMgrError e) {
        	System.err.println("LEX_ERROR: " + e.getMessage());
        }
        catch (ParseException e) {
        	System.err.println("SINT_ERROR: " + e.getMessage());
        } 
    }
}
PARSER_END(adac)
//------------ descripción del AL
SKIP:
{
	" "
|	"\t"
|	"\n"
	
}
//---------------------------------------------------------
TOKEN_MGR_DECLS : {
	//lo declarado aquí estará en "ejemploTokenManager"
	//también estará, por ejemplo, "lengthOfMatch", "image", ...
	static int nPalInt = 0;
	static int valNums = 0;
	static void infoToken(String mensaje, Token token){
		System.out.println("(" + token.beginLine + "," + token.beginColumn + "): " + mensaje + " " + token.image);
	}
}

SPECIAL_TOKEN : {
	< tCOM: "--"(~["\n"])*"\n" >
}

TOKEN:
{
 	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
|	< #ALNUM: (< LETTER > | < DIGIT >) >
/*|   < tCOM: "--"(~["\n"])*"\n" >*/
}

TOKEN:
{
	< tPOPEN: "(" >
|	< tPCLOSE: ")" >	
|	< tC2 : "\"" >
|	< tC1 : "\'" >
|	< tCOMA : "," >
|	< tPC: ";" >
|	< tCORCHETEOPEN : "[">
|	< tCORCHETECLOSE : "]" >
|	< tNUM : (< DIGIT >)+ >
//|	< tLETTER: < LETTER > >

}

TOKEN:
{
 	< tBEGIN: "begin" >
|	< tBOOL: "boolean" >
|	< tCHAR: "character" >
| 	< tINT: "integer" >
|	< tFUNC: "function" >
|	< tPROC: "procedure" >
|	< tIS : "is" >
|	< tVAL :  "val" >
|	< tREF : "ref" >
|	< tRET : "return" >
|	< tINT2CHAR : "int2char" >
|	< tCHAR2INT : "char2int" >
}

TOKEN:
{
	< tIF: "if" >
|	< tTHEN: "then" >
|	< tELSE: "else" >
|	< tEND: "end" >
|	< tWHILE: "while" >
|	< tDO: "do" >
}

TOKEN:
{
	< tASIG: ":=" >
|	< tPUTLINE: "put_line" >
|	< tPUT: "put" >
|	< tGET: "get" >
}

TOKEN: 
{
	< tAND: "and">
|	< tOR: "or">
|	< tNOT: "not" >
|	< tMAYEQ: ">=" >
|	< tMENEQ: "<=" >
|	< tEQ: "=" >
|	< tMAY: ">" >
|	< tMEN : "<" >
|	< tDIST : "<>" >
|	< tTRUE : "true" >
|	< tFALSE : "false" >
}

TOKEN: 
{
	< tSUM: "+">
|	< tRES: "-">
|	< tMUL: "*">
|	< tDIV: "div">	
|	< tMOD: "mod">
|	< tSTRING : "\"" ("\"\"")* (("\"\"")* (~["\""])*)* "\"" >
|	< tCARACTER : "\'" (~[])? "\'" >
}


TOKEN:
{
/*	< tIDERR: ((< LETTER > | ( "_"(< ALNUM >)+)+ )*(< ALNUM > | "_"(< ALNUM >)+)*"__"( < ALNUM > | "_" (< ALNUM >)+ )*) | (< DIGIT > ( "_" | <ALNUM> )*) >
		{
			System.out.println("ERROR LÉXICO: (" + matchedToken.beginLine + "," + matchedToken.beginColumn + "): símbolo no reconocido " + matchedToken.image);
		}
|*/	< tID:  (< LETTER > | ("_"(< ALNUM >)+)+ ) ( < ALNUM > | "_"(< ALNUM >)+ )* >
}

SPECIAL_TOKEN : {
/*	< tCOM: "--"(~["\n"])*"\n" >
|*/	< tERROR: ~[] >
		{
			System.out.println("ERROR LÉXICO: (" + matchedToken.beginLine + "," + matchedToken.beginColumn + "): símbolo no reconocido " + matchedToken.image);
		}
}

void S () : 
{

}
{
	prog()
	< EOF >
}

//------------ Símbolo inicial de la gramática. Para análisis léxico no hace falta más
void prog () : 
{

}
{
  	<tPROC>
  	<tID>
  	<tIS>
 	declaracion_variables()
  	declaracion_procs_funcs()
	bloque_sentencias()
}

void declaracion_variables():
{

}
{
	( declaracion() <tPC> )*
}

void declaracion():
{

}
{
	tipo_variable() 
	lista_vars()
}
void tipo_variable():
{

}
{
	<tINT> | <tCHAR> | <tBOOL>
}

void lista_vars():
{

}
{
	variable() ( <tCOMA> variable() )*
}

void variable():
{

}
{
	LOOKAHEAD(2)
	<tID> <tCORCHETEOPEN> <tNUM> <tCORCHETECLOSE>
| 	<tID>
}

void declaracion_procs_funcs():
{

}
{
	( declaracion_proc_func() )*
}



void declaracion_proc_func():
{

}
{

	(cabecera_procedimiento() | cabecera_funcion())
	declaracion_variables() bloque_sentencias()
	
}

void cabecera_procedimiento():
{
	
}
{
	<tPROC> <tID> <tPOPEN> lista_parametros() <tPCLOSE> <tIS>
}

void cabecera_funcion():
{
	
}
{
	<tFUNC> tipo_variable() <tID> <tPOPEN> lista_parametros() <tPCLOSE> <tIS>
}

void lista_parametros():
{

}
{
	(parametro())*
}

void parametro():
{

}
{
	(<tVAL> | <tREF>) tipo_variable() lista_vars() 
}

void bloque_sentencias():
{
	
}
{
	<tBEGIN>
	(inst() )*
	<tEND>
}

void inst():
{

}
{
	(inst_asignacion() 
//|	inst_leer()
//|	inst_escribir()
//|	inst_saltar_linea()
//|	inst_escribir_linea()
//|	LOOKAHEAD(2) inst_invoc_proc()
//|	inst_seleccion()

|	inst_return()	
)
	<tPC>
	|	inst_iteracion()
}

void inst_asignacion():
{

}
{
	variable()
	<tASIG>
	expresion()
}

void inst_iteracion():
{

}
{
	<tWHILE>
	expresion()
	<tDO>
	(inst())*
	<tEND>
}

void inst_return():
{

}
{
	<tRET> expresion()
}

void lista_cero_o_mas_exps():
{

}
{
	( lista_una_o_mas_exps() )?
}

void lista_una_o_mas_exps():
{

}
{
	expresion() ( lista_una_o_mas_exps() )?
}

void expresion():
{

}
{
	expresion_simple() ( operador_relacional() expresion_simple() )?
}

void operador_relacional():
{

}
{
	<tEQ>
|	<tMAYEQ>
|	<tMENEQ>
|	<tMAY>
|	<tMEN>
|	<tDIST>
}

void expresion_simple():
{

}
{
	( <tSUM> | <tRES> )?
	termino() (operador_aditivo() termino())*
}

void operador_aditivo():
{
	
}
{
	<tSUM>
|	<tRES>
| 	<tOR>
}

void termino():
{

}
{
	factor() ( operador_multiplicativo() factor())*
}


void operador_multiplicativo():
{

}
{
	<tMUL>
|	<tMOD>
|	<tDIV>
|	<tAND>
}

void factor():
{
	
}
{
	<tNOT> factor()
|	<tPOPEN> expresion() <tPCLOSE>
|	<tINT2CHAR> <tPOPEN> expresion() <tPCLOSE> 
|	<tCHAR2INT> <tPOPEN> expresion() <tPCLOSE>
|	LOOKAHEAD(2) <tID> <tPOPEN> lista_cero_o_mas_exps() <tPCLOSE>
|	LOOKAHEAD(2) <tID> <tCORCHETEOPEN> expresion() <tCORCHETECLOSE>
|	<tID>
| 	<tNUM>
| 	<tCARACTER>
| 	<tSTRING>
| 	<tTRUE>
| 	<tFALSE>
}