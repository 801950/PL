-- Ordenación por fusión
-- el fichero contiene como primer elemento la cantidad de números que se quieren ordenar y a continuación la secuencia de número enteros que se van a ordenar
--------------------------------------------------------------------------
procedure test_2 is
--------------------------------------------------------------------------
    integer v[500000];
    integer n;
--------------------------------------------------------------------------
    -- imprime por pantalla las n primeras componentes del vector
    procedure imprimir(ref integer v[500000]; val integer n) is
        integer i;
    begin
        i := 0;
        while i < n do
            put(v[i], " ");
            i := i + 1;
        end
        put_line();
    end

    -- lee de la entrada estandar la cantidad de números que se van a leer
    -- lee a continuación, de la entrada estándar n números y los guarda en el vector v
    function boolean cargar_vector(ref integer n; ref integer v[500000]) is
        integer i;
    begin
        get(n);
        if n > 500000 then
            return false;
        else
            i := 0;
            while i < n do
                get(v[i]);
                i := i + 1;
            end
            return true;
        end
    end

    -- procedimiento de fusión para el método de ordenación por fusión
    procedure fusion(ref integer v[500000]; val integer inf, med, sup) is
        integer h, i, j, k;
        integer b[500000];
    begin
        h := inf;
        i := 1;
        j := med + 1;
        while (h <= med) and (j <= sup) do
            if v[h] <= v[j] then
                b[i] := v[h];
                h := h + 1;
            else 
                b[i] := v[j];
                j := j + 1;
            end
            i := i + 1;
        end
        if h > med then
            k := j;
            while k <= sup do
                b[i] := v[k];
                i := i + 1;
                k := k + 1;
            end
        else 
            k := h;
            while k <= med do
                b[i] := v[k];
                i := i + 1;
                k := k + 1;
            end
        end
        k := inf;
        while k <= sup do
            v[k] := b[k-inf+1];
            k := k + 1;
        end
    end

    -- método de ordenación por fusión
    procedure ordenar(ref integer v[500000]; val integer inf,sup) is
        integer medio;
    begin
        if inf < sup then
            medio := (inf + sup) div 2;
            ordenar(v, inf, medio);
            ordenar(v, medio + 1, sup);
            fusion(v, inf, medio, sup);
        end
    end
begin
    if cargar_vector(n,v) then
        imprimir(v,n);
        ordenar(v,0,n-1);
        imprimir(v,n);
    end
end